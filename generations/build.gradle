buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "snapshot_20171007"
    useDepAts = true
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name 'sponge-repo'
        url 'https://repo.spongepowered.org/repository/sponge-legacy'
    }
    maven {
        name 'Generations Maven Repo'
        url 'http://maven.pixelmongenerations.com/repository/maven-releases'
    }
}

dependencies {
    shadow project(":api")
    shadow project(":sponge")

    shadow 'com.pixelmongenerations:main-mod:8.4.2'
    shadow 'org.spongepowered:spongeapi:7.3.0'
    shadow 'net.kyori:adventure-api:4.3.0'
    shadow 'net.kyori:adventure-text-serializer-legacy:4.3.0'
    shadow 'net.impactdev.impactor:sponge:3.1.2'
    shadow 'org.slf4j:slf4j-api:1.7.25'

    compile 'net.impactdev:pixelmonbridge:1.0.3-SNAPSHOT'
    compile 'net.impactdev.pixelmonbridge:generations:1.0.3-SNAPSHOT'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    archiveName = "GTS-Extension-Generations-${project.version}.jar"

    relocate ('net.impactdev.pixelmonbridge', 'net.impactdev.gts.relocations.pixelmonbridge')
    relocate ('net.kyori.adventure', 'net.impactdev.impactor.relocations.adventure')
    relocate ('net.kyori.examination', 'net.impactdev.impactor.relocations.examination')
}

sourceMainJava.finalizedBy(blossomSourceReplacementJava)
blossom {
    blossomSourceReplacementJava.output = file("$buildDir/sources/main/java")
    replaceToken '@version@', version
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}